# Week 6 - Homework Sheet

```agda
{-# OPTIONS --without-K --safe #-}

module exercises.my-homework6 where

private
 open import prelude hiding (Bool-elim)
 open import natural-numbers-functions hiding (_â‰¤_ ; is-even ; +-assoc ; +-comm)
 open import List-functions
 open import decidability
 open import isomorphisms
```
We will also want to use some things from the Lab of Week 4:

```agda
 open import exercises.lab4-solutions
```

## Part I: More on length

Besides `map`, the function `reverse` is another example of a length-preserving
operation.

```agda
 length-of-++ : {A : Type} (xs ys : List A)
              â†’ length (xs ++ ys) â‰¡ length xs + length ys
 length-of-++ []        ys = refl (length ys)
 length-of-++ (x :: xs) ys = ap suc IH
   where
    IH : length (xs ++ ys) â‰¡ length xs + length ys
    IH = length-of-++ xs ys

 +-comm : (x y : â„•) â†’ x + y â‰¡ y + x
 +-comm 0       y = 0 + y       â‰¡âŸ¨ refl _ âŸ©
                    y           â‰¡âŸ¨ sym (+-base y) âŸ©
                    y + 0       âˆ

 +-comm (suc x) y = suc x + y   â‰¡âŸ¨ refl _ âŸ©
                    suc (x + y) â‰¡âŸ¨ ap suc (+-comm x y) âŸ©
                    suc (y + x) â‰¡âŸ¨ refl _ âŸ©
                    suc y + x   â‰¡âŸ¨ sym (+-step y x) âŸ©
                    y + suc x   âˆ

 length-of-reverse : {A : Type} (xs : List A)
                   â†’ length (reverse xs) â‰¡ length xs
 length-of-reverse [] = refl zero
 length-of-reverse (x :: xs) =
   length (reverse xs ++ (x :: []))                 â‰¡âŸ¨ length-of-++ (reverse xs) (x :: []) âŸ©
   length (reverse xs) + length (reverse (x :: [])) â‰¡âŸ¨ refl _ âŸ©
   length (reverse xs) + 1                          â‰¡âŸ¨ +-comm (length (reverse xs)) 1 âŸ©
   1 + length (reverse xs)                          â‰¡âŸ¨ refl _ âŸ©
   suc (length (reverse xs))                        â‰¡âŸ¨ ap suc (length-of-reverse xs) âŸ©
   suc (length xs) âˆ

```

**Prove** the above.

## Part II: More on isomorphisms

### Exercise 2a

```agda
 â„•-[â‹†]-iso : â„• â‰… List ğŸ™
 â„•-[â‹†]-iso = record { bijection = f ; bijectivity = f-is-bijection }
  where
   f : â„• â†’ List ğŸ™
   f zero = []
   f (suc n) = â‹† :: (f n)

   g : List ğŸ™ â†’ â„•
   g [] = 0
   g (_ :: xs) = suc (g xs)

   gf : g âˆ˜ f âˆ¼ id
   gf zero = refl zero
   gf (suc n) = ap suc (gf n)

   fg : f âˆ˜ g âˆ¼ id
   fg [] = refl []
   fg (â‹† :: xs) = ap (â‹† ::_) (fg xs)

   f-is-bijection : is-bijection f
   f-is-bijection = record { inverse = g ; Î· = gf ; Îµ = fg }
```

**Show** that the type of natural numbers `â„•` is isomorphic to the type of lists
over the unit type `ğŸ™`.

Hint: The statement of Exercise 2b may help you.

### Exercise 2b

```agda
 open _â‰…_

 â„•â†’[â‹†]-preserves-length : (n : â„•) â†’ length (bijection â„•-[â‹†]-iso n) â‰¡ n
 â„•â†’[â‹†]-preserves-length zero = refl zero
 â„•â†’[â‹†]-preserves-length (suc n) = ap suc (â„•â†’[â‹†]-preserves-length n)
```

Notice how `bijection` extracts the function `f` you defined in `â„•-[â‹†]-iso`.

**Prove** that for any `n : â„•`, the length of the list `f n : List ğŸ™`
(where `f : â„• â†’ List ğŸ™` is as you defined in Exercise 3a) is `n`.

## Part III: More on evenness

In this exercise, we will continue where we left off in the lab exercises on
evenness. Recall the predicates `is-even` and `check-even`:

```agda
 is-even : â„• â†’ Type
 is-even x = Î£ y ê‰ â„• , x â‰¡ 2 * y
```

```agda
 check-even : â„• â†’ Bool
 check-even zero          = true
 check-even (suc zero)    = false
 check-even (suc (suc n)) = check-even n
```

Now recall the discussion about decidable predicates that we had in the previous
weeks. When you proved that `check-even` and `is-even` are logically equivalent
in the lab, you have in fact implicitly proved that `is-even` is a decidable
predicate! In this exercise, we will make this implicit proof _explicit_.

**Complete** the remaining holes in the following proof outline; starting with
proving a lemma stating that a Boolean is either `true` or `false`.

```agda
 +-suc-on-right : (x y : â„•) â†’ x + suc y â‰¡ suc (x + y)
 +-suc-on-right 0       y = refl (suc y)
 +-suc-on-right (suc x) y = ap suc (+-suc-on-right x y)

 evenness-lemmaâ‚‚ : (n : â„•) â†’ is-even n â†’ is-even (2 + n)
 evenness-lemmaâ‚‚ n (k , p) = suc k , goal
  where
   goal : 2 + n â‰¡ 2 * suc k
   goal = 2 + n         â‰¡âŸ¨ ap (suc âˆ˜ suc) p âŸ©
          2 + (k + k)   â‰¡âŸ¨ ap suc (sym (+-suc-on-right k k)) âŸ©
          suc k + suc k âˆ

 check-evenâ‡’even : (n : â„•) â†’ check-even n â‰¡ true â†’ is-even n
 check-evenâ‡’even zero prf = zero , refl zero
 check-evenâ‡’even (suc (suc n)) prf = evenness-lemmaâ‚‚ n (check-evenâ‡’even n prf)

 not-1â‰¡suc-suc : (n : â„•) â†’ Â¬(1 â‰¡ suc (n + suc n))
 not-1â‰¡suc-suc zero ()
 not-1â‰¡suc-suc (suc n) ()

 evenness-lemmaâ‚ : (n : â„•) â†’ is-even (2 + n) â†’ is-even n
 evenness-lemmaâ‚ n (suc k , p) = k , goal
  where
   subgoal : suc (suc n) â‰¡ suc (suc (2 * k))
   subgoal = suc (suc n)       â‰¡âŸ¨ p âŸ©
             suc k + suc k     â‰¡âŸ¨ ap suc (+-suc-on-right k k) âŸ©
             suc ((suc k) + k) âˆ

   goal : n â‰¡ 2 * k
   goal = suc-is-injective (suc-is-injective subgoal)

 evenâ‡’check-even : (n : â„•) â†’ is-even n â†’ check-even n â‰¡ true
 evenâ‡’check-even zero e = refl true
 evenâ‡’check-even (suc zero) (suc zero , ())
 --(suc n/2 , prf) = ğŸ˜-elim (not-1â‰¡suc-suc n/2 prf)
 evenâ‡’check-even (suc (suc n)) e = evenâ‡’check-even n (evenness-lemmaâ‚ n e)

 principle-of-bivalence : (b : Bool) â†’ (b â‰¡ true) âˆ” (b â‰¡ false)
 principle-of-bivalence true = inl (refl true)
 principle-of-bivalence false = inr (refl false)

 false-is-not-true : Â¬ (false â‰¡ true)
 false-is-not-true ()

 is-even-is-decidable : (n : â„•) â†’ is-decidable (is-even n)
 is-even-is-decidable n =
  âˆ”-nondep-elim goalâ‚ goalâ‚‚ (principle-of-bivalence (check-even n))
   where
    goalâ‚ : check-even n â‰¡ true â†’ is-decidable (is-even n)
    goalâ‚ p = inl (check-evenâ‡’even n p)

    goalâ‚‚ : check-even n â‰¡ false â†’ is-decidable (is-even n)
    goalâ‚‚ p = inr subgoal
     where
      subgoal : Â¬ is-even n
      subgoal q = false-is-not-true (sym (trans (sym (evenâ‡’check-even n q)) p))
```

## Part IV: Stretcher exercises on length

*The following two exercises are rather hard and are should be interesting to
students that like a challenge.*

Recall that we can define `filter` as
```agda
 filter : {A : Type} â†’ (A â†’ Bool) â†’ List A â†’ List A
 filter P []        = []
 filter P (x :: xs) = if P x then (x :: ys) else ys
  where
   ys = filter P xs
```

We also remind you of the inductively defined less-than-or-equal relation `â‰¤`
on the natural numbers.

```agdacode
data _â‰¤_ : â„• â†’ â„• â†’ Type where
  â‰¤-zero : (  y : â„•) â†’ 0 â‰¤ y
  â‰¤-suc  : (x y : â„•) â†’ x â‰¤ y â†’ suc x â‰¤ suc y
```

Finally, the following lemmas are provided to you for your convenience.

```agda
 â‰¤-suc-lemma : (n : â„•) â†’ n â‰¤ (1 + n)
 â‰¤-suc-lemma 0       = â‰¤-zero (1 + 0)
 â‰¤-suc-lemma (suc n) = goal
  where
   IH : n â‰¤ (1 + n)
   IH = â‰¤-suc-lemma n
   goal : suc n â‰¤ suc (suc n)
   goal = â‰¤-suc n (suc n) IH

 Bool-elim : (A : Bool â†’ Type)
           â†’ A false
           â†’ A true
           â†’ (x : Bool) â†’ A x
 Bool-elim A xâ‚€ xâ‚ false = xâ‚€
 Bool-elim A xâ‚€ xâ‚ true  = xâ‚
```

### Exercise 4a (stretcher ğŸŒ¶)

**Prove** that filtering a list decreases its length.

```agda
 length-of-filter : {A : Type} (P : A â†’ Bool) (xs : List A)
                  â†’ length (filter P xs) â‰¤ length xs
 length-of-filter P [] = â‰¤-zero zero
 length-of-filter P (x :: xs) with P x
 length-of-filter P (x :: xs) | true = â‰¤-suc (length (filter P xs)) (length xs) (length-of-filter P xs)
 length-of-filter P (x :: xs) | false = â‰¤-trans (length (filter P xs)) (length xs) (suc (length xs)) (length-of-filter P xs) (â‰¤-suc-lemma (length xs))

 length-of-filter' : {A : Type} (P : A â†’ Bool) (xs : List A)
                   â†’ length (filter P xs) â‰¤ length xs
 length-of-filter' P [] = â‰¤-zero zero
 length-of-filter' P (x :: xs) = Bool-elim goal-statement false-case true-case (P x)
   where
     filtered-tail = filter P xs
--     length (filter P (x :: xs)) â‰¤ length (x :: xs)
     goal-statement : Bool â†’ Type
     goal-statement b = length (if b then (x :: filtered-tail) else filtered-tail) â‰¤ length (x :: xs)

     IH : length filtered-tail â‰¤ length xs
     IH = length-of-filter P xs

     false-case : length filtered-tail â‰¤ length (x :: xs)
     false-case = â‰¤-trans _ _ _ IH (â‰¤-suc-lemma (length xs))

     true-case : length (x :: filtered-tail) â‰¤ length (x :: xs)
     true-case = â‰¤-suc _ _ IH
```

*Hints*:
 - You can use `â‰¤-trans` from the [sample solutions to
   Lab 4](lab4-solutions.lagda.md) if you need that `â‰¤` is transitive.
   (The sample solutions are already imported for you.)
 - Think about how to use `Bool-elim`.

### Exercise 4b (stretcher ğŸŒ¶ğŸŒ¶)

Given a predicate `P : A â†’ Bool` and a list `xs : List A`, we could filter `xs`
by `P` and by `not âˆ˜ P`. If we do this and compute the lengths of the resulting
lists, then we expect their sum to be equal to the length of the unfiltered list
`xs`. **Prove** this fact.

```agda
 length-of-filters : {A : Type} (P : A â†’ Bool) (xs : List A)
                   â†’ length (filter P xs) + length (filter (not âˆ˜ P) xs)
                   â‰¡ length xs
 length-of-filters P [] = refl zero
 length-of-filters P (x :: xs) with P x
 length-of-filters P (x :: xs) | true = ap suc (length-of-filters P xs)
 length-of-filters P (x :: xs) | false =
   length (filter P xs) + suc (length (filter (not âˆ˜ P) xs)) â‰¡âŸ¨ +-suc-on-right _ _ âŸ©
   suc (length (filter P xs) + (length (filter (not âˆ˜ P) xs))) â‰¡âŸ¨ ap suc (length-of-filters P xs) âŸ©
   suc (length xs) âˆ

 length-of-filters' : {A : Type} (P : A â†’ Bool) (xs : List A)
                    â†’ length (filter P xs) + length (filter (not âˆ˜ P) xs)
                    â‰¡ length xs
 length-of-filters' P [] = refl zero
 length-of-filters' P (x :: xs) = Bool-elim goal-statement false-case true-case (P x)
   where
     ys = filter P xs
     ys' = filter (not âˆ˜ P) xs

     IH : length ys + length ys' â‰¡ length xs
     IH = length-of-filters' P xs
 
     goal-statement : Bool â†’ Type
     goal-statement b = length (if b then (x :: ys) else ys) +
                        length (if (not b) then (x :: ys') else ys')
                      â‰¡ suc (length xs)

     true-case : length (x :: ys) + length ys' â‰¡ suc (length xs)
     true-case = ap suc IH

     false-case : length ys + length (x :: ys') â‰¡ suc (length xs)
     false-case =
       length ys + length (x :: ys') â‰¡âŸ¨ refl _ âŸ©
       length ys + suc (length ys') â‰¡âŸ¨ +-suc-on-right _ _ âŸ©
       suc (length ys + length ys') â‰¡âŸ¨ ap suc IH âŸ©
       suc (length xs) âˆ
```

*Hint*: You can use associativity (`+-assoc`) and commutativity (`+-comm`) from
 the sample solutions to last week's exercises. (The necessary files are already
 imported for you.)

